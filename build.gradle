plugins {
    // apply plugin for OSGi R6/DS 1.3 support
    id 'org.dm.bundle' version '0.8.4'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'groovy' // tests only
apply plugin: 'maven-publish'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'net.kemuri9.sling'
version = '0.1-SNAPSHOT'

dependencies {
    compileOnly "org.osgi:org.osgi.service.component.annotations:${osgiDSVersion}"
    compileOnly "org.osgi:org.osgi.service.metatype.annotations:${osgiDSVersion}"
    compileOnly 'org.osgi:osgi.annotation:6.0.1'

    //compile 'commons-io:commons-io:1.4'
    //compile 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.commons-io:1.4_3'
    compile 'org.apache.sling:org.apache.sling.api:2.14.0'
    compile 'org.apache.sling:org.apache.sling.commons.classloader:1.3.2'
    compile 'org.apache.sling:org.apache.sling.settings:1.3.8'
    compile "org.osgi:osgi.core:${osgiVersion}"
    compile "org.osgi:osgi.cmpn:${osgiVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testCompile 'org.apache.sling:org.apache.sling.testing.osgi-mock:2.0.2'
    testCompile 'org.apache.sling:org.apache.sling.testing.sling-mock:1.6.2'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    testRuntime "org.slf4j:slf4j-simple:${slf4jVersion}"

    // debugging
    //testRuntime 'org.apache.sling:org.apache.sling.resourceaccesssecurity:1.0.0'
    testRuntime 'org.apache.sling:org.apache.sling.auth.core:1.3.14'
    testRuntime 'org.apache.sling:org.apache.sling.resourceresolver:1.4.18'
    testRuntime 'org.apache.sling:org.apache.sling.servlets.resolver:2.4.2'
    testRuntime 'org.apache.sling:org.apache.sling.discovery.standalone:1.0.2'
}

sourceSets.main.resources {
    srcDir file('.') include 'LICENSE'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            pom.withXml {
                Node root = asNode()
                // add name
                root.children().add(4, new Node(null, 'name', 'Sling file system ResourceProvider'))
                // add description
                root.children().add(5, new Node(null, 'description', 'A ResourceProvider for the Sling platform that utilizes the filesystem for storage of resources'))
                // add packaging
                root.children().add(6, new Node(null, 'packaging', 'jar'))
                // change scopes from compile to provided (gradle only generates dependency entries for compile scope dependencies)
                root.get('dependencies').get(0).get('dependency')*.get('scope')*.get(0)*.value = 'provided'
            }
        }
    }
}

bundle {
    instruction 'Export-Package', '!*.impl, net.kemuri9.*'
    instruction 'Bundle-Name', 'Filesystem Resource Provider'
}
